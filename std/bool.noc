/*
Bool functions
*/

load stack.noc

def if = {
  ---
  if statement
  
  (example)
  ["else"] ["then"] [True] if => ["then"]
  ---
  [
    [[True] [swap pop unquote]]
    [[False] [pop unquote]]
  ] case
}

def not = {
    ---
    Logical operator
    Get False if the value is True and True if value is False

    (example)
    True not => [False]
    False not => [True]
    ---
    [
        [[True] [False]]
        [[False] [True]]
    ] case
}

def isEqual = {
    ---
    Check if 2 values are equal 

    (example)
    5 6 isEqual => [False]
    [Right "ok"] [Right "ok"] isEqual => [True]
    ---
    quote quote [[True]] cat quote [[[_] [False]]] cat case
}